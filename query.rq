PREFIX : <https://id.kb.se/vocab/>
PREFIX marc: <https://id.kb.se/marc/>
PREFIX lge: <https://id.kb.se/language/>
PREFIX saogf: <https://id.kb.se/term/saogf/>
PREFIX country: <https://id.kb.se/country/>

SELECT DISTINCT 
  ?work ?title ?givenName ?familyName ?lifeSpan ?author ?isbn ?genre 
{
  ?instance a ?type ;
    :instanceOf ?work ;
    :hasTitle [ :mainTitle ?title ] ;
    :publication [ :year ?year ; :country country:sw ] .
  OPTIONAL { ?instance :identifiedBy [ a :ISBN ; :value ?isbn ] }

  FILTER(?type IN (:Instance, :Print))
  FILTER(STR(?year) = "|YEAR|")

  # Fetch the unique work that this instance is of
  ?work a ?workType ;
    :language lge:swe ;
    :genreForm ?genre ;
    :contribution [ a :PrimaryContribution ; :agent ?author ] .
  FILTER(?workType IN (:Text, :Work, :TextualWork))
  # Ignore translated works
  FILTER NOT EXISTS { ?work :translationOf [] }
  FILTER NOT EXISTS { ?work :hasPart [ :translationOf [] ] }
  # Ignore children's books
  FILTER NOT EXISTS { ?work :intendedAudience marc:Juvenile }

  # Work must contain at least one of the following genres
  FILTER EXISTS { 
    ?work :genreForm ?wantedGenre .
    FILTER(?wantedGenre IN(
      marc:FictionNotFurtherSpecified,
      marc:HumorSatiresEtc,
      marc:Novel,
      marc:ShortStory,
      saogf:Noveller,
      saogf:Romaner
    ))
  }

  # Get author information
  ?author :givenName ?givenName ; :familyName ?familyName
  OPTIONAL { ?author :lifeSpan ?lifeSpan }
}