PREFIX : <https://id.kb.se/vocab/>
PREFIX marc: <https://id.kb.se/marc/>
PREFIX lge: <https://id.kb.se/language/>

SELECT DISTINCT 
  ?work ?instance ?title ?givenName ?familyName ?lifeSpan ?author ?isbn ?genre 
{
  ?instance a ?type ;
    :instanceOf ?work ;
    :hasTitle [ :mainTitle ?title ] ;
    :publication [ :year ?year ] .
  OPTIONAL { ?instance :identifiedBy [ a :ISBN ; :value ?isbn ] }

  FILTER(?type IN (:Instance, :Print))
  FILTER(STR(?year) = "|YEAR|")

  # Fetch the unique work that this instance is of
  ?work a ?workType ;
    :language lge:swe ;
    :genreForm ?genre ;
    :contribution [ a :PrimaryContribution ; :agent ?author ] .
  FILTER(?workType IN (:Text, :Work, :TextualWork))
  # Ignore translated works
  FILTER NOT EXISTS { ?work :translationOf [] }

  # Get author information
  ?author :givenName ?givenName ; :familyName ?familyName
  OPTIONAL { ?author :lifeSpan ?lifeSpan }
}