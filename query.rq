PREFIX : <https://id.kb.se/vocab/>
PREFIX marc: <https://id.kb.se/marc/>
PREFIX lge: <https://id.kb.se/language/>
PREFIX saogf: <https://id.kb.se/term/saogf/>
PREFIX country: <https://id.kb.se/country/>
PREFIX nat: <https://id.kb.se/nationality/>

SELECT
  ?instance ?work ?bib ?imageHost ?title ?author ?givenName ?familyName ?lifeSpan ?isni ?isbn ?genre
{
  ?instance a ?type ;
    :instanceOf ?work ;
    :hasTitle [ :mainTitle ?title ] ;
    :publication [ :year ?year ; :country country:sw ] .
  OPTIONAL { ?instance :identifiedBy [ a :ISBN ; :value ?isbn ] }
  # Fetch cover image if it exists
  # We need the publisher, which is the image host, to know what URL are available for this work
  OPTIONAL { ?instance :image [ a :ImageObject ; :publisher ?imageHost ] }
  OPTIONAL { ?instance :sameAs ?bib }
  FILTER(?type IN (:Instance, :Print))
  FILTER(?year = "|YEAR|")

  # Fetch the unique work that this instance is of
  ?work a ?workType ;
    :language lge:swe ;
    :genreForm ?genre ;
    :contribution [ a :PrimaryContribution ; :agent ?author ] .
  FILTER(?workType IN (:Text, :Work, :TextualWork))
  # Ignore translated works
  FILTER NOT EXISTS { ?work :translationOf [] }
  FILTER NOT EXISTS { ?work :hasPart [ :translationOf [] ] }
  # Ignore children's books
  FILTER NOT EXISTS { ?work :intendedAudience marc:Juvenile }

  # Keep only works where Swedish is the ONLY language
  FILTER NOT EXISTS {
    ?work :language ?otherLang .
    FILTER(?otherLang != lge:swe)
  }

  # Work must contain at least one of the following genres
  FILTER EXISTS { 
    ?work :genreForm ?wantedGenre .
    FILTER(?wantedGenre IN(
      marc:FictionNotFurtherSpecified,
      marc:HumorSatiresEtc,
      marc:Novel,
      marc:ShortStory,
      saogf:Noveller,
      saogf:Romaner
    ))
  }

  # Get author information
  ?author :givenName ?givenName ;
          :familyName ?familyName ;
          # Must be a swedish author
          :nationality nat:e-sw---
  OPTIONAL { ?author :lifeSpan ?lifeSpan }
  OPTIONAL { ?author :identifiedBy [ a :ISNI ; :value ?isni ]}
}