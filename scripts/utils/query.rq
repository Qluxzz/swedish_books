PREFIX : <https://id.kb.se/vocab/>
PREFIX marc: <https://id.kb.se/marc/>
PREFIX lge: <https://id.kb.se/language/>
PREFIX saogf: <https://id.kb.se/term/saogf/>
PREFIX country: <https://id.kb.se/country/>
PREFIX nat: <https://id.kb.se/nationality/>

SELECT
  ?instance ?work ?bib ?title ?author ?givenName ?familyName ?lifeSpan ?isni ?isbn ?mainInstancePages ?otherInstancePages ?imageYear ?imageHost ?imageBib ?imageIsbn ?genre
{
  ?instance a ?type ;
    :instanceOf ?work ;
    :hasTitle [ :mainTitle ?title ] ;
    :publication [ :year ?year ; :country country:sw ] .

  # Page count from this instance
  OPTIONAL { ?instance :extent [ a :Extent ; :label ?mainInstancePages ] }

  # Page count from any instance of the same work
  OPTIONAL { ?otherInstance :instanceOf ?work ; :extent [ a :Extent ; :label ?otherInstancePages ] }

  OPTIONAL { ?instance :identifiedBy [ a :ISBN ; :value ?isbn ] }
  OPTIONAL { ?instance :sameAs ?bib }

  OPTIONAL {
    ?imageInstance 
      :instanceOf ?work ;
      :image [ :publisher ?imageHost ] ;
      :publication [ :year ?imageYear ] .
    OPTIONAL { ?imageInstance :sameAs ?imageBib }
    OPTIONAL { ?imageInstance :identifiedBy [ a :ISBN ; :value ?imageIsbn ] }
  }

  FILTER(?type IN (:Instance, :Print))
  FILTER(?year = "|YEAR|")

  # Fetch the unique work that this instance is of
  ?work a ?workType ;
    :language lge:swe ;
    :genreForm ?genre ;
    :contribution [ a :PrimaryContribution ; :agent ?author ] .
  FILTER(?workType IN (:Text, :Work, :TextualWork))
  # Ignore translated works
  FILTER NOT EXISTS { ?work :translationOf [] }
  # Ignore work if any part is translated, we only want original swedish works
  FILTER NOT EXISTS { ?work :hasPart [ :translationOf [] ] }
  # Ignore work if any contributor is a translator, since this means the work is translated
  FILTER NOT EXISTS { 
    ?work :contribution [ a :Contribution ; :role ?otherRole ]
    FILTER(?otherRole != <https://id.kb.se/relator/translator>)
  }
  # Ignore children's books
  FILTER NOT EXISTS { ?work :intendedAudience marc:Juvenile }

  # Keep only works where Swedish is the ONLY language
  FILTER NOT EXISTS {
    ?work :language ?otherLang .
    FILTER(?otherLang != lge:swe)
  }

  # Work must contain at least one of the following genres
  FILTER EXISTS { 
    ?work :genreForm ?wantedGenre .
    FILTER(?wantedGenre IN(
      marc:FictionNotFurtherSpecified,
      marc:HumorSatiresEtc,
      marc:Novel,
      marc:ShortStory,
      saogf:Noveller,
      saogf:Romaner
    ))
  }

  # Get author information
  ?author :givenName ?givenName ;
          :familyName ?familyName ;
          # Must be a swedish author
          :nationality nat:e-sw---
  OPTIONAL { ?author :lifeSpan ?lifeSpan }
  OPTIONAL { ?author :identifiedBy [ a :ISNI ; :value ?isni ]}
}